import java.text.SimpleDateFormat
import groovy.xml.XmlParser

apply plugin: 'com.android.application'

android {
    compileSdk = 36
    buildToolsVersion '36.0.0'
    defaultConfig {
        applicationId "com.osfans.mcpdict"
        minSdkVersion 24 //SQLite 3.9.2
        targetSdkVersion 34
        versionCode 101
        def date = new SimpleDateFormat ("yyyyMMdd").format(new Date())
        versionName "6.0 ($date)"
        buildConfigField "int", "DB_VER", "$date"

        ndk {
            abiFilters "arm64-v8a"
        }

        // ndkVersion "28.2.13676358" // "27.0.12077973"
        externalNativeBuild {
            cmake {
                targets "libopencc"
                arguments "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON", "-DCMAKE_BUILD_TYPE=Release"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
            // version "4.1.1" // "3.22.1"
        }
    }

    signingConfigs {
        release {
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def stringsFile = android.sourceSets.main.res.sourceFiles.find { it.name.equals 'strings.xml' }
            def appName = new XmlParser().parse(stringsFile).string.find { it.@name.equals 'app_name' }.text()
            def versionName = new SimpleDateFormat ("yyyyMMdd").format(new Date())
            outputFileName = "${appName}${versionName}.APK"
        }
    }
    
    def propsFile = rootProject.file('keystore.properties') //store release config in local.properties
    def configName = 'release'

    if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props!=null && props.containsKey('storeFile')) {
            android.signingConfigs.named(configName) {
                storeFile = rootProject.file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    namespace 'com.osfans.mcpdict'
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.webkit:webkit:1.14.0'
    implementation 'com.github.MKergall:osmbonuspack:6.9.0'
    // Fix Duplicate class
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.github.requery:sqlite-android:3.49.0'
    implementation 'me.zhanghai.android.fastscroll:library:1.3.0'
}
